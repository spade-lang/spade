/// Group elements from two arrays `[l0, l1, l2...]` and `[r0, r1, r2...]`
/// to form `[(l0, r0), (l1, r1), (l2, r2)...]`
pub fn zip<L, R, #uint N>(l: [L; N], r: [R; N]) -> [(L, R); N] {
    gen if N == 0 {
        []
    } else {
        [(l[0], r[0])] `concat_arrays` (l[1..N] `zip` r[1..N])
    }
}

/// Interleave the elements of two arrays `[l0, l1, l2...]`
/// and `[r0, r1, r2...]` to form `[l0, r0, l1, r1, l2, r2...]`
pub fn interleave_arrays<T, #uint N, #uint ON>(l: [T; N], r: [T; N]) -> [T; ON]
    where ON: {2 * N}
{
    gen if N == 0 {
        []
    } else {
        [l[0], r[0]] `concat_arrays` (l[1..N] `interleave_arrays` r[1..N])
    }
}

impl<T, #uint N> [T; N] {
    pub fn concat<#uint M, #uint OL>(self, other: [T; M]) -> [T; OL]
        where OL: {M + N}
    {
        self `concat_arrays` other
    }

    pub fn map<F, O>(self, f: F) -> [O; N]
        where F: Fn(T) -> O
    {
        gen if N == 0 {
            []
        } else {
            [f.call((self[0],))].concat(self[1..N].map(f))
        }
    }

    pub fn zip<O>(self, other: [O; N]) -> [(T, O); N] {
        self `zip` other
    }

    pub fn interleave<#uint ON>(self, other: Self) -> [T; ON]
        where ON: {2 * N}
    {
        self `interleave_arrays` other
    }
}
